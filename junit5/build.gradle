plugins {
    id 'com.android.test'
}

android {
    targetProjectPath ':app'

    compileSdkVersion = rootProject.compileSdkVersion.toInteger()
    buildToolsVersion = rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion project.compileSdkVersion

        testInstrumentationRunner "com.epam.reportportal.android.junit5.AndroidJUnit5ReportPortalRunner"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        animationsDisabled=true
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
        exclude 'META-INF/INDEX.LIST'
    }
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    implementation project(':app')

    // Android libraries for testing Activities
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Android Espresso libraries
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Report Portal libraries
    implementation ('com.epam.reportportal:agent-android-junit5:5.1.1') {
        exclude group: 'org.aspectj' // AspectJ is already included by Android
    }
    implementation ('com.epam.reportportal:logger-java-logback:5.1.4') {
        exclude group: 'com.epam.reportportal'
    }

    // Logging support, newer versions of logback do not support Android
    implementation 'ch.qos.logback:logback-classic:1.2.11'

    // android-junit5 necessary libraries
    implementation 'androidx.test:runner:1.5.2'
    implementation 'de.mannodermaus.junit5:android-test-core:1.3.0'
    implementation 'de.mannodermaus.junit5:android-test-runner:1.3.0'

    // JUnit5 libraries, 'junit-jupiter-api' is inherited from agent
    implementation "org.junit.platform:junit-platform-runner:${project.junit5RunnerVersion}"
    implementation "org.junit.jupiter:junit-jupiter-engine:${project.junit5Version}"

    // JUnit5 (Optional) If you need "Parameterized Tests"
    implementation "org.junit.jupiter:junit-jupiter-params:${project.junit5Version}"
}
